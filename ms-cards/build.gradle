plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '3.4.3'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.hidetake.swagger.generator' version '2.19.2'
}

group = 'com.payments.cards'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'com.solace.spring.boot:solace-java-spring-boot-starter:5.2.0'
    implementation 'jakarta.validation:jakarta.validation-api:2.0.2'
    implementation 'com.theokanning.openai-gpt3-java:client:0.18.2'
    implementation 'com.theokanning.openai-gpt3-java:service:0.18.2'
    implementation 'io.springfox:springfox-swagger2:3.0.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.11.0'
    implementation 'org.everit.json:org.everit.json.schema:1.5.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
    swaggerCodegen 'io.swagger.codegen.v3:swagger-codegen-cli:3.0.52'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-docker-compose'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:mongodb'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

swaggerSources{
    payment {
        inputFile = file('src/main/resources/api/credit-card.yaml')
        code {
            language = 'spring'
            outputDir = file('build/swagger')
            components = ['models']
            configFile = file('src/main/resources/api/config.json')
        }
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = swaggerSources.payment.code.outputDir
    }
}

compileJava.dependsOn(swaggerSources.payment.code)
sourceSets.main.java.srcDir "${swaggerSources.payment.code.outputDir}/src/main/java"

tasks.named('test') {
    useJUnitPlatform()
}
